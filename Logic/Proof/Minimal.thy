section {* Minimal Logic *}
  
theory Minimal
  imports  "../Combinator"
begin

text {* This theory presents \emph{minimal logic}, the implicational fragment of 
        intuitionistic logic. *}

subsection {* Axiomatization *}

text {* Minimal logic is given by the following Hilbert-style axiom system: *}

class Minimal_Logic =
  fixes implication :: "'a \<Rightarrow> 'a \<Rightarrow> 'a"    (infixr "\<rightarrow>" 70)
  fixes proves :: "'a \<Rightarrow> bool"             ("\<turnstile>\<^sub>M\<^sub>I\<^sub>N _" [60] 55)
  assumes Axiom_1: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<phi> \<rightarrow> \<psi> \<rightarrow> \<phi>"
  assumes Axiom_2: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N (\<phi> \<rightarrow> \<psi> \<rightarrow> \<chi>) \<rightarrow> (\<phi> \<rightarrow> \<psi>) \<rightarrow> \<phi> \<rightarrow> \<chi>"
  assumes Modus_Ponens: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<phi> \<rightarrow> \<psi>  \<Longrightarrow> \<turnstile>\<^sub>M\<^sub>I\<^sub>N \<phi> \<Longrightarrow> \<turnstile>\<^sub>M\<^sub>I\<^sub>N \<psi>"     
    
text {* One instance is the built-in type {@type "bool"}; instantiating this type establishes
        consistency. *}

interpretation Bool_Minimal_Logic: Minimal_Logic "op \<longrightarrow>" "id"
proof qed (simp)+

subsection {* Common Rules *}

lemma (in Minimal_Logic) flip_implication: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N (\<phi> \<rightarrow> \<psi> \<rightarrow> \<chi>) \<rightarrow> \<psi> \<rightarrow> \<phi> \<rightarrow> \<chi>"  
  by (meson Axiom_1 Axiom_2 Modus_Ponens)  
  
lemma (in Minimal_Logic) hypothetical_syllogism: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N (\<psi> \<rightarrow> \<chi>) \<rightarrow> (\<phi> \<rightarrow> \<psi>) \<rightarrow> \<phi> \<rightarrow> \<chi>"  
  by (meson Axiom_1 Axiom_2 Modus_Ponens)

lemma (in Minimal_Logic) implication_absorption: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N (\<phi> \<rightarrow> \<phi> \<rightarrow> \<psi>) \<rightarrow> \<phi> \<rightarrow> \<psi>"  
  by (meson Axiom_1 Axiom_2 Modus_Ponens)
  
subsection {* The Curry Howard Correspondence *}

text {* The (polymorphic) typing for a combinator @{term X} is given by the 
        relation @{term "X \<^bold>\<Colon> \<phi>"}, which is defined in @{theory Combinator}. *}

text {* Combinator types form an instance of minimal logic. *}

interpretation Combinator_Minimal_Logic: Minimal_Logic "op \<^bold>\<Rightarrow>" "\<lambda> \<phi>. \<exists> X. X \<^bold>\<Colon> \<phi>"
proof qed (meson Simply_Typed_SKCombinator.intros)+

text {* The minimal logic generated by combinator logic is \emph{free} in the following sense: 
        If @{term "X \<^bold>\<Colon> \<phi>"} holds for some combinator @{term "X"} then @{term "\<phi>"} may be
        interpreted as logical consequence in any given minimal logic instance. *}  

text {* The fact that any valid type in combinator logic may be intrepreted in minimal logic is
        a form of the \emph{Curry-Howard correspondence}. TODO: Cite *}

primrec (in Minimal_Logic) Combinator_Simple_Type_interpretation 
                           :: "'a Combinator_Simple_Type \<Rightarrow> 'a" ("\<^bold>\<lparr> _ \<^bold>\<rparr>" [50]) where
     "\<^bold>\<lparr> Atom p \<^bold>\<rparr> = p"
   | "\<^bold>\<lparr> \<phi> \<^bold>\<Rightarrow> \<psi> \<^bold>\<rparr> = \<^bold>\<lparr> \<phi> \<^bold>\<rparr> \<rightarrow> \<^bold>\<lparr> \<psi> \<^bold>\<rparr>"

lemma (in Minimal_Logic) Curry_Howard_correspondence: "X \<^bold>\<Colon> \<phi> \<Longrightarrow> \<turnstile>\<^sub>M\<^sub>I\<^sub>N \<^bold>\<lparr> \<phi> \<^bold>\<rparr>"
  by (induct rule: Simply_Typed_SKCombinator.induct, 
      (simp add: Axiom_1 Axiom_2 Modus_Ponens)+)

subsection {* Lists of Assumptions *}

subsubsection {* List Implication *}  
  
text {* Implication given a list of assumptions can be expressed recursively *}  
  
primrec (in Minimal_Logic) list_implication :: "'a list \<Rightarrow> 'a \<Rightarrow> 'a" (infix ":\<rightarrow>" 80) where
    "[] :\<rightarrow> \<phi> = \<phi>"
  | "(\<psi> # \<Psi>) :\<rightarrow> \<phi> = \<psi> \<rightarrow> \<Psi> :\<rightarrow> \<phi>"

subsubsection {* Definition of Deduction *}

text {* Deduction from a list of assumptions can be expressed in terms of @{term "op :\<rightarrow>"}. *}
  
abbreviation (in Minimal_Logic) list_proves :: "'a list \<Rightarrow> 'a \<Rightarrow> bool" (infix ":\<turnstile>" 60) where
  "\<Gamma> :\<turnstile> \<phi> \<equiv> \<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Gamma> :\<rightarrow> \<phi>"    
    
subsubsection {* Interpretation as Minimal Logic *}

text {* The relation @{term "op :\<turnstile>"} may naturally be interpreted as a @{term "proves"} 
        predicate for an instance of minimal logic for a fixed list of assumptions @{term "\<Gamma>"}. *}
  
text {* Analogues of the two axioms of minimal logic can be naturally stated using
        list implication. *}

lemma (in Minimal_Logic) list_implication_Axiom_1: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<phi> \<rightarrow> \<Gamma> :\<rightarrow> \<phi>"
  by (induct \<Gamma>, (simp, meson Axiom_1 Axiom_2 Modus_Ponens)+)

lemma (in Minimal_Logic) list_implication_Axiom_2: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Gamma> :\<rightarrow> (\<phi> \<rightarrow> \<psi>) \<rightarrow> \<Gamma> :\<rightarrow> \<phi> \<rightarrow> \<Gamma> :\<rightarrow> \<psi>"
  by (induct \<Gamma>, (simp, meson Axiom_1 Axiom_2 Modus_Ponens hypothetical_syllogism)+)

text {* The lemmas @{thm list_implication_Axiom_1} and  @{thm list_implication_Axiom_2} jointly 
        give rise to an interpretation of minimal logic, where a list of assumptions 
        @{term "\<Gamma>"} plays the role of a \emph{background theory} of @{term "op :\<turnstile>"}. *}
  
context Minimal_Logic begin
interpretation List_Deduction_Logic: Minimal_Logic "op \<rightarrow>" "\<lambda> \<phi>. \<Gamma> :\<turnstile> \<phi>"
proof qed (meson Axiom_1 Axiom_2 Modus_Ponens list_implication_Axiom_1 list_implication_Axiom_2)+
end    

subsection {* The Deduction Theorem *}

text {* One result in the meta-theory of minimal logic is the \emph{deduction theorem},
        which is a mechanism for moving antecedents back and forth from collections of 
        assumptions. *}

text {* To develop the deduction theorem, the following two lemmas generalize 
        @{thm "flip_implication"}. *}
    
lemma (in Minimal_Logic) list_flip_implication1: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N (\<phi> # \<Gamma>) :\<rightarrow> \<chi> \<rightarrow> \<Gamma> :\<rightarrow> (\<phi> \<rightarrow> \<chi>)"
  by (induct \<Gamma>, 
      (simp, meson Axiom_1 Axiom_2 Modus_Ponens flip_implication hypothetical_syllogism)+)    
    
lemma (in Minimal_Logic) list_flip_implication2: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Gamma> :\<rightarrow> (\<phi> \<rightarrow> \<chi>) \<rightarrow> (\<phi> # \<Gamma>) :\<rightarrow> \<chi>"
  by (induct \<Gamma>, 
      (simp, meson Axiom_1 Axiom_2 Modus_Ponens flip_implication hypothetical_syllogism)+)

text {* Together the two lemmas above suffice to prove a form of the deduction theorem: *}
    
theorem (in Minimal_Logic) list_deduction_theorem: "(\<phi> # \<Gamma>) :\<turnstile> \<psi> \<equiv> \<Gamma> :\<turnstile> \<phi> \<rightarrow> \<psi>"
  by (smt Modus_Ponens list_flip_implication1 list_flip_implication2)
  
subsection {* Monotonic Growth in Deductive Power *}

text {* In logic, for two sets of assumptions @{term "\<Phi>"} and @{term "\<Psi>"},
        if @{term "\<Psi> \<subseteq> \<Phi>"} then the latter theory @{term "\<Phi>"} is said to be \emph{stronger}
        than former theory @{term "\<Psi>"}.  In principle, anything a weaker theory can prove a 
        stronger theory can prove.  One way of saying this is that deductive power increases
        monotonically with as the set of underlying assumptions grow. *}

text {* The monotonic growth of deductive power can be expressed as a meta-theorem 
        in minimal logic. *}
  
text {* The lemma @{thm "list_flip_implication2"} presents a means of \emph{introducing}
        assumptions into a list of assumptions when those assumptions have arrived at an 
        implication.  The next lemma presents a means of \emph{discharging} those assumptions,
        which can be used in the monotonic growth theorem to be proved. *}
  
lemma (in Minimal_Logic) list_implication_removeAll:
  "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Gamma> :\<rightarrow> \<psi> \<rightarrow> (removeAll \<phi> \<Gamma>) :\<rightarrow> (\<phi> \<rightarrow> \<psi>)"  
proof -
  have "\<forall> \<psi>. \<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Gamma> :\<rightarrow> \<psi> \<rightarrow> (removeAll \<phi> \<Gamma>) :\<rightarrow> (\<phi> \<rightarrow> \<psi>)"
  proof(induct \<Gamma>)
    case Nil
    then show ?case by (simp, meson Axiom_1)
  next
    case (Cons \<chi> \<Gamma>)
    assume inductive_hypothesis: "\<forall> \<psi>. \<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Gamma> :\<rightarrow> \<psi> \<rightarrow> removeAll \<phi> \<Gamma> :\<rightarrow> (\<phi> \<rightarrow> \<psi>)"
    moreover {
      assume "\<phi> \<noteq> \<chi>"
      with inductive_hypothesis
      have "\<forall> \<psi>. \<turnstile>\<^sub>M\<^sub>I\<^sub>N (\<chi> # \<Gamma>) :\<rightarrow> \<psi> \<rightarrow> removeAll \<phi> (\<chi> # \<Gamma>) :\<rightarrow> (\<phi> \<rightarrow> \<psi>)"
        by (simp, meson Modus_Ponens hypothetical_syllogism) 
    }
    moreover {
      fix \<psi>
      assume \<phi>_equals_\<chi>: "\<phi> = \<chi>"
      moreover with inductive_hypothesis 
      have "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Gamma> :\<rightarrow> (\<chi> \<rightarrow> \<psi>) \<rightarrow> removeAll \<phi> (\<chi> # \<Gamma>) :\<rightarrow> (\<phi> \<rightarrow> \<chi> \<rightarrow> \<psi>)" by simp
      hence "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Gamma> :\<rightarrow> (\<chi> \<rightarrow> \<psi>) \<rightarrow> removeAll \<phi> (\<chi> # \<Gamma>) :\<rightarrow> (\<phi> \<rightarrow> \<psi>)"
        by (metis calculation Modus_Ponens implication_absorption list_flip_implication1 
                  list_flip_implication2 list_implication.simps(2))
      ultimately have "\<turnstile>\<^sub>M\<^sub>I\<^sub>N (\<chi> # \<Gamma>) :\<rightarrow> \<psi> \<rightarrow> removeAll \<phi> (\<chi> # \<Gamma>) :\<rightarrow> (\<phi> \<rightarrow> \<psi>)"
        by (simp, metis Modus_Ponens hypothetical_syllogism list_flip_implication1 
                        list_implication.simps(2))
    }
    ultimately show ?case by simp 
  qed
  thus ?thesis by blast
qed

text {* From lemma above presents what is needed to prove that deductive power for lists is
        monotonic. *}
  
theorem (in Minimal_Logic) list_implication_monotonic:
  "set \<Sigma> \<subseteq> set \<Gamma> \<Longrightarrow> \<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Sigma> :\<rightarrow> \<phi> \<rightarrow> \<Gamma> :\<rightarrow> \<phi>"
proof -
  assume "set \<Sigma> \<subseteq> set \<Gamma>"
  moreover have "\<forall> \<Sigma> \<phi>. set \<Sigma> \<subseteq> set \<Gamma> \<longrightarrow> \<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Sigma> :\<rightarrow> \<phi> \<rightarrow> \<Gamma> :\<rightarrow> \<phi>"
  proof(induct \<Gamma>)
    case Nil
    then show ?case
      by (metis list_implication.simps(1) list_implication_Axiom_1 set_empty subset_empty)  
  next
    case (Cons \<psi> \<Gamma>)
    assume inductive_hypothesis: "\<forall>\<Sigma> \<phi>. set \<Sigma> \<subseteq> set \<Gamma> \<longrightarrow> \<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Sigma> :\<rightarrow> \<phi> \<rightarrow> \<Gamma> :\<rightarrow> \<phi>"
    {
      fix \<Sigma> :: "'a list"
      fix \<phi> :: "'a"
      assume \<Phi>_subset_relation: "set \<Sigma> \<subseteq> set (\<psi> # \<Gamma>)"
      have "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Sigma> :\<rightarrow> \<phi> \<rightarrow> (\<psi> # \<Gamma>) :\<rightarrow> \<phi>"
      proof -
        {
          assume "set \<Sigma> \<subseteq> set \<Gamma>"
          hence ?thesis
            by (metis inductive_hypothesis Axiom_1 Modus_Ponens flip_implication 
                      list_implication.simps(2)) 
        }
        moreover {
          let ?\<Delta>\<Phi> = "removeAll \<psi> \<Sigma>"
          assume "~ (set \<Sigma> \<subseteq> set \<Gamma>)"
          hence "set ?\<Delta>\<Phi> \<subseteq> set \<Gamma>" using \<Phi>_subset_relation by auto
          hence "\<turnstile>\<^sub>M\<^sub>I\<^sub>N ?\<Delta>\<Phi> :\<rightarrow> (\<psi> \<rightarrow> \<phi>) \<rightarrow> \<Gamma> :\<rightarrow> (\<psi> \<rightarrow> \<phi>)" using inductive_hypothesis by auto
          hence "\<turnstile>\<^sub>M\<^sub>I\<^sub>N ?\<Delta>\<Phi> :\<rightarrow> (\<psi> \<rightarrow> \<phi>) \<rightarrow> (\<psi> # \<Gamma>) :\<rightarrow> \<phi>"
            by (metis Modus_Ponens flip_implication list_flip_implication2 list_implication.simps(2))
          moreover have "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<Sigma> :\<rightarrow> \<phi> \<rightarrow> ?\<Delta>\<Phi> :\<rightarrow> (\<psi> \<rightarrow> \<phi>)"
            by (simp add: local.list_implication_removeAll)
          ultimately have ?thesis
            using Modus_Ponens hypothetical_syllogism by blast 
        }
        ultimately show ?thesis by blast
     qed
    }
    thus ?case by simp
  qed
  ultimately show ?thesis by simp 
qed

text {* A direct consequence is that deduction from lists of assumptions is monotonic as well: *}

theorem (in Minimal_Logic) list_deduction_monotonic:
  "set \<Sigma> \<subseteq> set \<Gamma> \<Longrightarrow> \<Sigma> :\<turnstile> \<phi> \<Longrightarrow> \<Gamma> :\<turnstile> \<phi>"
  using Modus_Ponens list_implication_monotonic by blast
  
subsection {* The Deduction Theorem Revisited *}
  
text {* The monotonic nature of deduction allows us to prove another form of the deduction
        theorem, where the assumption being discharged is completely removed from the list of
        assumptions. *}

theorem (in Minimal_Logic) alternate_list_deduction_theorem:
  "(\<phi> # \<Gamma>) :\<turnstile> \<psi> \<equiv> (removeAll \<phi> \<Gamma>) :\<turnstile> \<phi> \<rightarrow> \<psi>"
  by (smt Modus_Ponens 
          filter_is_subset 
          list_deduction_monotonic 
          list_deduction_theorem 
          list_implication_removeAll 
          removeAll.simps(2) 
          removeAll_filter_not_eq)

subsection {* Reflection *}

text {* In logic the \emph{reflection} principle sometimes refers to when a collection of
        assumptions can deduce any of its members. *}

lemma (in Minimal_Logic) list_deduction_reflection: "\<phi> \<in> set \<Gamma> \<Longrightarrow> \<Gamma> :\<turnstile> \<phi>"
  by (metis insert_subset 
            list.simps(15) 
            list_deduction_monotonic 
            list_implication.simps(2) 
            list_implication_Axiom_1 
            order_refl)

subsection {* The Cut Rule *}

text {* \emph{Cut} is a rule commonly presented in sequent calculi, dating back to Gerhard 
        Gentzen's "Investigations in Logical Deduction" (1934) TODO: Cite me *}

text {* The cut rule is not generally necessary in sequent calculi and it can often be shown
        that the rule can be eliminated without reducing the power of the underlying logic.
        However, as demonstrated by George Boolos' "Don't Eliminate Cut" (1984) (TODO: Cite me), 
        removing the rule can often lead to very inefficient proof systems. *}

text {* Here the rule is presented just as a meta theorem. *}

theorem (in Minimal_Logic) list_deduction_cut_rule: "(\<phi> # \<Delta>) :\<turnstile> \<psi> \<Longrightarrow> \<Gamma> :\<turnstile> \<phi> \<Longrightarrow> \<Gamma> @ \<Delta> :\<turnstile> \<psi>"
  by (metis Modus_Ponens 
            Un_iff 
            list_deduction_monotonic 
            list_deduction_theorem 
            list_implication_Axiom_2 
            set_append subsetI)
  
text {* The cut rule can also be strengthened to entire lists of propositions. *}    
    
theorem (in Minimal_Logic) strong_list_deduction_cut_rule: 
  "(\<Phi> @ \<Delta>) :\<turnstile> \<psi> \<Longrightarrow> \<forall> \<phi> \<in> set \<Phi>. \<Gamma> :\<turnstile> \<phi> \<Longrightarrow> (\<Gamma> @ \<Delta>) :\<turnstile> \<psi>"
proof -
  have "\<forall> \<psi>. ((\<Phi> @ \<Delta>) :\<turnstile> \<psi> \<longrightarrow> (\<forall> \<phi> \<in> set \<Phi>. \<Gamma> :\<turnstile> \<phi>) \<longrightarrow> (\<Gamma> @ \<Delta>) :\<turnstile> \<psi>)"
    proof(induct \<Phi>)
      case Nil
      then show ?case
        by (metis Un_iff append.left_neutral list_deduction_monotonic set_append subsetI)
    next
      case (Cons \<chi> \<Phi>)
      assume inductive_hypothesis: "\<forall> \<psi>. \<Phi> @ \<Delta> :\<turnstile> \<psi> \<longrightarrow> (\<forall>\<phi>\<in>set \<Phi>. \<Gamma> :\<turnstile> \<phi>) \<longrightarrow> \<Gamma> @ \<Delta> :\<turnstile> \<psi>"
      {
        fix \<psi> \<chi> :: "'a"
        assume "(\<chi> # \<Phi>) @ \<Delta> :\<turnstile> \<psi>"
        hence A: "\<Phi> @ \<Delta> :\<turnstile> \<chi> \<rightarrow> \<psi>"  using list_deduction_theorem by auto
        assume "\<forall>\<phi> \<in> set (\<chi> # \<Phi>). \<Gamma> :\<turnstile> \<phi>"
        hence B: "\<forall> \<phi> \<in> set \<Phi>. \<Gamma> :\<turnstile> \<phi>" 
          and C: "\<Gamma> :\<turnstile> \<chi>" by auto
        from A B have "\<Gamma> @ \<Delta> :\<turnstile> \<chi> \<rightarrow> \<psi>" using inductive_hypothesis by blast
        with C have "\<Gamma> @ \<Delta> :\<turnstile> \<psi>" using list_deduction_monotonic
          by (metis Modus_Ponens UnCI list_implication_Axiom_2 set_append subset_iff) 
      }
      thus ?case by simp
    qed
  moreover assume "(\<Phi> @ \<Delta>) :\<turnstile> \<psi>"
  moreover assume "\<forall> \<phi> \<in> set \<Phi>. \<Gamma> :\<turnstile> \<phi>"    
  ultimately show ?thesis by blast
qed
    
section {* Sets of Assumptions *}

text {* While deduction in terms of lists of assumptions is straight-forward to define,
        deduction (and the \emph{deduction theorem}) is commonly given in terms of \emph{sets}
        of propositions.  This formulation is suited to establishing strong completeness theorems
        and compactness theorems. *}  

subsection {* Definition of Deduction *}  

text {* Just as deduction from a list @{term "op :\<turnstile>"} can be defined in terms of @{term "op :\<rightarrow>"},
        deduction from a \emph{set} of assumptions can be expressed in terms of @{term "op :\<turnstile>"}. *}
  
abbreviation (in Minimal_Logic) set_proves :: "'a set \<Rightarrow> 'a \<Rightarrow> bool" (infix "\<tturnstile>" 60) where
  "\<Gamma> \<tturnstile> \<phi> \<equiv> \<exists> \<Psi>. set(\<Psi>) \<subseteq> \<Gamma> \<and> \<Psi> :\<turnstile> \<phi>" 

subsubsection {* Interpretation as Minimal Logic *}  
  
text {* As in the case of @{term "op :\<turnstile>"}, the relation @{term "op \<tturnstile>"} may be interpreted as 
        a @{term "proves"} predicate for a fixed set of assumptions @{term "\<Gamma>"}. *}

text {* The following lemma is given in order to establish this, which asserts that
        every minimal logic tautology @{term "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<phi>"} is also a tautology for @{term "\<Gamma> \<tturnstile> \<phi>"}. *}

lemma (in Minimal_Logic) set_deduction_weaken: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<phi> \<Longrightarrow> \<Gamma> \<tturnstile> \<phi>"
  by (metis empty_subsetI list.set(1) local.list_implication.simps(1))

text {* In the case of the empty set, the converse may be established. *}

lemma (in Minimal_Logic) set_deduction_base_theory: "\<turnstile>\<^sub>M\<^sub>I\<^sub>N \<phi> \<equiv> {} \<tturnstile> \<phi>"
  by simp

text {* Next, a form of \emph{modus ponens} is provided for @{term "op \<tturnstile>"}. *}

lemma (in Minimal_Logic) set_deduction_modus_ponens: "\<Gamma> \<tturnstile> \<phi> \<rightarrow> \<psi> \<Longrightarrow> \<Gamma> \<tturnstile> \<phi> \<Longrightarrow> \<Gamma> \<tturnstile> \<psi>"
proof -
  assume "\<Gamma> \<tturnstile> \<phi> \<rightarrow> \<psi>"
  then obtain \<Phi> where A: "set \<Phi> \<subseteq> \<Gamma>" and B: "\<Phi> :\<turnstile> \<phi> \<rightarrow> \<psi>" by auto
  assume "\<Gamma> \<tturnstile> \<phi>"
  then obtain \<Psi> where C: "set \<Psi> \<subseteq> \<Gamma>" and D: "\<Psi> :\<turnstile> \<phi>" by auto
  from B D have "\<Psi> @ \<Phi> :\<turnstile> \<psi>"
    using list_deduction_cut_rule list_deduction_theorem by blast 
  moreover have "set (\<Psi> @ \<Phi>) \<subseteq> \<Gamma>" by (simp add: A C)
  ultimately show ?thesis by blast
qed

context Minimal_Logic begin
interpretation Set_Deduction_Logic: Minimal_Logic "op \<rightarrow>" "\<lambda> \<phi>. \<Gamma> \<tturnstile> \<phi>"
proof
   fix \<phi> \<psi>
   show "\<Gamma> \<tturnstile> \<phi> \<rightarrow> \<psi> \<rightarrow> \<phi>"  by (metis Axiom_1 set_deduction_weaken)
next
    fix \<phi> \<psi> \<chi>
    show "\<Gamma> \<tturnstile> (\<phi> \<rightarrow> \<psi> \<rightarrow> \<chi>) \<rightarrow> (\<phi> \<rightarrow> \<psi>) \<rightarrow> \<phi> \<rightarrow> \<chi>"  by (metis Axiom_2 set_deduction_weaken)
next
    fix \<phi> \<psi>
    show "\<Gamma> \<tturnstile> \<phi> \<rightarrow> \<psi> \<Longrightarrow> \<Gamma> \<tturnstile> \<phi> \<Longrightarrow> \<Gamma> \<tturnstile> \<psi>" using set_deduction_modus_ponens by auto
qed
end

subsection {* The Deduction Theorem *}

lemma (in Minimal_Logic) set_deduction_theorem: "insert \<phi> \<Gamma> \<tturnstile> \<psi> \<equiv> \<Gamma> \<tturnstile> \<phi> \<rightarrow> \<psi>"
proof -
  have "\<Gamma> \<tturnstile> \<phi> \<rightarrow> \<psi> \<Longrightarrow> insert \<phi> \<Gamma> \<tturnstile> \<psi>" 
    by (metis insert_mono list.simps(15) list_deduction_theorem)
  moreover {
    assume "insert \<phi> \<Gamma> \<tturnstile> \<psi>"
    then obtain \<Phi> where A: "set \<Phi> \<subseteq> insert \<phi> \<Gamma>" and B: "\<Phi> :\<turnstile> \<psi>" by auto
    from A have C: "set (removeAll \<phi> \<Phi>) \<subseteq> \<Gamma>" by auto
    moreover from B have D: "removeAll \<phi> \<Phi> :\<turnstile> \<phi> \<rightarrow> \<psi>"
      using Modus_Ponens list_implication_removeAll by blast 
    ultimately have "\<Gamma> \<tturnstile> \<phi> \<rightarrow> \<psi>" by blast 
  }
  ultimately show "insert \<phi> \<Gamma> \<tturnstile> \<psi> \<equiv> \<Gamma> \<tturnstile> \<phi> \<rightarrow> \<psi>" by smt
qed
end